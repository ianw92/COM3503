#version 330 core

in vec3 fragPos;
in vec3 ourNormal;
in vec2 ourTexCoord;

out vec4 fragColor;

uniform vec3 viewPos;

struct PointLight {
  vec3 position;

  float constant;
  float linear;
  float quadratic;

  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
};
#define NR_POINT_LIGHTS 3
uniform PointLight pointLights[NR_POINT_LIGHTS];
vec3 calcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir);

struct Material {
  sampler2D diffuse;
  sampler2D specular;
  float shininess;
};

uniform Material material;

vec3 calcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir) {
  vec3 lightDir = normalize(light.position - fragPos);
  // diffuse shading
  float diff = max(dot(normal, lightDir), 0.0);
  // specular shading
  vec3 reflectDir = reflect(-lightDir, normal);
  float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
  // attenuation
  float distance    = length(light.position - fragPos);
  float attenuation = 1.0 / (light.constant + light.linear * distance +
              light.quadratic * (distance * distance));
  // combine results
  vec3 ambient  = light.ambient  * vec3(texture(material.diffuse, ourTexCoord));
  vec3 diffuse  = light.diffuse  * diff * vec3(texture(material.diffuse, ourTexCoord));
  vec3 specular = light.specular * spec * vec3(texture(material.specular, ourTexCoord));
  ambient  *= attenuation;
  diffuse  *= attenuation;
  specular *= attenuation;
  return (ambient + diffuse + specular);
}

void main() {
  // properties
  vec3 norm = normalize(ourNormal);
  vec3 viewDir = normalize(viewPos - fragPos);
  vec3 result = calcPointLight(pointLights[0], norm, fragPos, viewDir);
  result += calcPointLight(pointLights[1], norm, fragPos, viewDir);
  result += calcPointLight(pointLights[2], norm, fragPos, viewDir);

  //for(int i = 0; i < NR_POINT_LIGHTS; i++)
  //    result += calcPointLight(pointLights[i], norm, fragPos, viewDir);

  fragColor = vec4(result, 1.0);
}
